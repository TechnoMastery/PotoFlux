plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = "net.minheur.potoflux"
version = "beta-4.3"
sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // implementation "com.github.TechnoMastery:MinLib:v1.0" // minLib â†’ unused
    implementation 'com.google.code.gson:gson:2.11.0'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'net.minheur.potoflux.PotoFlux'
        )
    }
}

shadowJar {
    archiveFileName.set("PotoFlux-${version}.jar")
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'net.minheur.potoflux.PotoFlux'
    }
}

processResources {
    filesMatching("version.properties") {
        expand "version": version
    }
}

build.dependsOn shadowJar

tasks.register("run", JavaExec) {
    group = "application"
    description = "Run PotoFlux"
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("net.minheur.potoflux.PotoFlux")
    doLast {
        println("PotoFlux launched")
    }
}