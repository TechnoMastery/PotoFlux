plugins {
    id 'java'
}

group = "net.minheur.potoflux"
version = "6.1"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'com.google.code.gson:gson:2.11.0'

    // scan classes
    implementation "org.reflections:reflections:0.10.2"

    // dark theme
    implementation 'com.formdev:flatlaf:3.3'

}

jar {
    manifest {
        attributes(
                'Main-Class': 'net.minheur.potoflux.PotoFlux',
                'Implementation-Version': project.version
        )
    }
}

processResources {
    filesMatching("version.properties") {
        expand "version": version
    }
}

tasks.register("generateVersionProperties") {
    def outputDir = file("$buildDir/generated/resources")
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        def file = new File(outputDir, "version.properties")
        file.text = "version=${project.version}"
    }
}

sourceSets.main.resources.srcDir("$buildDir/generated/resources")

tasks.processResources.dependsOn(generateVersionProperties);

tasks.register("copyLibs", Copy) {
    dependsOn "build"

    group = "potoflux"
    description = "copy all libs to the build folder"

    doFirst {
        delete("$buildDir/libs/build")
    }

    from configurations.runtimeClasspath
    into "$buildDir/libs/build/lib/"
}

build.dependsOn jar

tasks.register("run", JavaExec) {
    group = "potoflux"
    description = "Run PotoFlux"

    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("net.minheur.potoflux.PotoFlux")

    args("devEnv")
}

tasks.register("copyJar", Copy) {
    dependsOn "copyLibs"

    group = "potoflux"
    description = "copy .jar file to build dir"

    from "$buildDir/libs/PotoFlux-$version" + ".jar"
    into "$buildDir/libs/build/"
}

tasks.register("msiPackage") {
    dependsOn "copyJar"
    group = "potoflux"
    description = "build as msi installer"

    doLast {
        exec {
            commandLine "jpackage",
            "--input", "$buildDir/libs/build",
            "--name", "PotoFlux",
            "--main-jar", "$buildDir/libs/build/PotoFlux-$version" + ".jar",
            "--main-class", "net.minheur.potoflux.PotoFlux",
            "--type", "msi",
            "--dest", "$buildDir/libs/output",
            "--win-dir-chooser",
            "--install-dir", "TechnoMastery/PotoFlux",
            "--win-shortcut",
            "--win-menu",
            "--java-options", "-Xmx512m",
            "--app-version", "$version",
            "--icon", "$buildDir/libs/main.ico",
            "--win-upgrade-uuid", "5536eb5f-55c0-408d-b034-677536a3a078"
        }
    }
}
